#! /usr/bin/env gforth
\                                           -*- forth -*-

require ./src/vocabulary.fs

get-current
also gbforth definitions
constant previous-wid

also gbforth

require ./src/cli.fs
require ./src/rom.fs
require ./src/sym.fs
require ./src/asm.fs
require ./src/runtime.fs
require ./src/compiler/cross.fs

also gb-assembler-impl
' rom, IS emit
' rom-offset IS offset
' rom! IS emit-to
previous

rom erase

output-file to-symbol-file set-sym-file
s" ; Generated by gbforth " sym-write sym-cr sym-cr

include ./src/cartridge.fs

fpath clear-path
gbforth-path fpath also-path
fpath path+ ./

require ./src/user.fs

( We want to load the input file sealed in the GBFORTH-USER
( vocabulary. The problem is that we do not want to populate this
  vocabulary with auxiliary words to load the input-file.

  Instead, we put all the words in this colon-definition to resolve
  the word into addresses before we seal and load the game.
)

: assert-empty-stack
  depth 0<> abort" Stack is not empty" ;

: write-game
  fix-header-complement
  fix-global-checksum
  output-file dump-rom ;

:noname
  assert-empty-stack

  gbforth-user definitions
  seal also

  --no-kernel invert if
    s" core.fs" required
  then

  input-file included

  --no-kernel invert if
    s" ./src/prelude.fs" included
  then

  assert-empty-stack
  write-game

  --verbose if
    ." \ " [asm] offset . [endasm]
    ." bytes written to "
    output-file type ."  written." CR
  then

  --debug if
    also forth
    also gbforth
  else
    bye
  then

; execute

( Do not write any code after this! )
